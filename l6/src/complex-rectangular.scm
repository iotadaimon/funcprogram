(define (install-rectangular-package)
    ;; Internal functions
  (define (real-part-rectangular z) (car z))
  (define (imag-part-rectangular z) (cdr z))
  (define (magnitude-rectangular z)
    (sqrt (+ (square (real-part-rectangular z))
             (square (imag-part-rectangular z)))))
  (define (angle-rectangular z)
    (atan (imag-part-rectangular z)
          (real-part-rectangular z)))
  (define (make-from-real-imag-rectangular x y)
    (cons x y))
  (define (make-from-mag-ang-rectangular r a)
    (cons (* r (cos a)) (* r (sin a))))

  ;; Interface to other parts
  (define (tag x) (attach-tag 'rectangular x))
  (put 'real-part '(rectangular) real-part-rectangular)
  (put 'imag-part '(rectangular) imag-part-rectangular)
  (put 'magnitude '(rectangular) magnitude-rectangular)
  (put 'angle '(rectangular) angle-rectangular)
  (put 'make-from-real-imag '(rectangular)
       (lambda (x y) (tag (make-from-real-imag-rectangular x y))))
  (put 'make-from-mag-ang '(rectangular)
       (lambda (r a) (tag (make-from-mag-ang-rectangular r a))))
  'done)
