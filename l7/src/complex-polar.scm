(define (install-polar-package)
  ;; Internal functions
  (define (real-part-polar z)
    (* (magnitude-polar z) (cos (angle-polar z))))
  (define (imag-part-polar z)
    (* (magnitude-polar z) (sin (angle-polar z))))
  (define (magnitude-polar z) (car z))
  (define (angle-polar z) (cdr z))
  (define (make-from-real-imag-polar x y)
    (cons (sqrt (+ (square x) (square y)))
          (atan y x)))
  (define (make-from-mag-ang-polar r a)
    (cons r a))
  
  ;; Interface to other parts
  (define (tag x) (attach-tag 'polar x))
  (put 'real-part '(polar) real-part-polar)
  (put 'imag-part '(polar) imag-part-polar)
  (put 'magnitude '(polar) magnitude-polar)
  (put 'angle '(polar) angle-polar)
  (put 'make-from-real-imag '(polar)
       (lambda (x y) (tag (make-from-real-imag-polar x y))))
  (put 'make-from-mag-ang '(polar)
       (lambda (r a) (tag (make-from-mag-ang-polar r a))))
'done)
